buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

project.version = scmVersion.version
project.group = "pl.allegro.finance"

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    implementation "com.google.guava:guava:32.1.1-jre"
    testImplementation "org.spockframework:spock-core:1.1-groovy-2.4"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        sonatype(MavenPublication) { publication ->
            from(components.findByName("java"))
            pom {
                name = 'Tradukisto'
                description = 'Small java library created to convert numbers to their word representations'
                url = 'https://github.com/allegro/tradukisto'
                inceptionYear = '2015'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dorians'
                        name = 'Dorian Sarnowski'
                    }
                    developer {
                      id = 'wjur'
                      name = 'Wojciech Jurczyk'
                    }
                }
                scm {
                    url = 'https://github.com/allegro/tradukisto'
                    connection = 'scm:git@github.com:allegro/tradukisto.git'
                    developerConnection = 'scm:git@github.com:allegro/tradukisto.git'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

if (System.getenv("GPG_KEY_ID")) {
    signing {
        useInMemoryPgpKeys(
            System.getenv("GPG_KEY_ID"),
            System.getenv("GPG_PRIVATE_KEY"),
            System.getenv("GPG_PRIVATE_KEY_PASSWORD")
        )
        sign publishing.publications
    }
}
